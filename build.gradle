apply plugin: "com.github.ben-manes.versions"

buildscript {
    ext {
        reachfive = [
            lib_version : "6.2.2",
            lib_group   : "co.reachfive.identity",
        ]

        kotlin_version = "1.5.31"
        kotlin_stdlib = "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        androidSettings = [
            min_sdk_version: 19,
            target_sdk_version: 29,
            compile_sdk_version: 29,
            version_code: 4
        ]

        libraries = [
            androidxAppCompat: "androidx.appcompat:appcompat:1.3.1",
            androidCustomTabs: "com.android.support:customtabs:28.0.0",
            result: "com.github.kittinunf.result:result:3.0.1",

            playServicesFido: "com.google.android.gms:play-services-fido:18.1.0",
            playServicesAuth: "com.google.android.gms:play-services-auth:19.2.0",

            retrofit:               "com.squareup.retrofit2:retrofit:2.9.0",
            retrofitConverterGson:  "com.squareup.retrofit2:converter-gson:2.9.0",
        ]
    }

    repositories {
        gradlePluginPortal()
        google()
    }

    dependencies {
        classpath "com.github.ben-manes:gradle-versions-plugin:0.39.0"

        classpath 'com.android.tools.build:gradle:7.0.2'
        classpath "com.google.gms:google-services:4.3.10"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
    }
}

group = reachfive.lib_group
version = reachfive.lib_version

subprojects {
    apply plugin: "com.android.library"
    apply plugin: "kotlin-android"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    repositories {
        google()
        mavenCentral()
    }

    android {
        compileSdkVersion androidSettings.compile_sdk_version

        defaultConfig {
            minSdkVersion androidSettings.min_sdk_version
            targetSdkVersion androidSettings.target_sdk_version
            versionCode androidSettings.version_code
            versionName this.version
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            }
        }

        lintOptions {
            abortOnError false
        }
    }

    afterEvaluate {
        publishing {
            repositories {
                maven {
                    name = "release"
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }

                maven {
                    name = "snapshot"
                    url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                    credentials {
                        username = ossrhUsername
                        password = ossrhPassword
                    }
                }
            }

            publications {
                release(MavenPublication) {
                    from components.release

                    pom {
                        name = 'ReachFive'
                        description = 'SDK'
                        inceptionYear = '2019'
                        url = 'https://github.com/ReachFive/identity-android-sdk'

                        groupId = this.group
                        version = this.version
                        packaging = 'aar'

                        licenses {
                            license {
                                name = 'The MIT License (MIT)'
                                url = 'https://opensource.org/licenses/MIT'
                            }
                        }

                        scm {
                            connection = 'https://github.com/ReachFive/identity-android-sdk.git'
                            url = 'https://github.com/ReachFive/identity-android-sdk'

                        }

                        developers {
                            developer {
                                id = 'reachfive'
                                name = 'Reachfive'
                            }
                        }
                    }
                }
            }
        }

        signing {
            sign publishing.publications.release
        }
    }
}
